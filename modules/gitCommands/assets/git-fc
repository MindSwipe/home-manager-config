#!/usr/bin/env fish
# a script to generate conventional commit messages

argparse 'm/message=' 'a/add' 'd/dry-run' -- $argv

# Get message from the argument or use the first positional argument
if set -ql _flag_m
    set message $_flag_m
else
    set message $argv[1]
end

if not test -n "$message"
    echo "message could not be set"
    return 1
end

echo "$message"

# Parse current branch, and set CC scope
set current_branch (git rev-parse --abbrev-ref HEAD)
string match -qr 'feature/(?<cc_scope>.*)' "$current_branch" -- $cc_scope

# Parse and pass any known arguments
set -l git_args

if set -ql _flag_a
    set -a git_args "-a"
end

# TODO make this a param?
set cc_type "feat"

if test -n "$cc_scope"
    set cc_type "$cc_type($cc_scope)" 
end

set -a git_args -m (string join ": " $cc_type $message)

if set -ql _flag_d
    echo "dry run: git commit $git_args"
else
    git commit $git_args
end