{
  lib,
  config,
  pkgs,
  ...
}:
{
  options.modules.treefmt = {
    enable = lib.mkEnableOption "Treefmt";
    additionalFormatters = lib.mkOption {
      type =
        with lib.types;
        listOf (submodule {
          options = {
            package = lib.mkOption {
              example = pkgs.nixfmt;
              type = package;
              description = "The Nix package to install that provides this formatter";
            };

            name = lib.mkOption {
              example = "nixfmt";
              type = str;
              description = "Formatter name";
            };

            command = lib.mkOption {
              example = "nixfmt";
              type = str;
              description = "Formatter command/ executable";
            };

            includes = lib.mkOption {
              example = [ "*.nix" ];
              type = listOf str;
              description = "List of files to format with this formatter";
            };

            excludes = lib.mkOption {
              default = [ ];
              example = [ ];
              type = listOf str;
              description = "List of files to *NOT* format with this formatter";
            };

            arguments = lib.mkOption {
              default = [ ];
              example = [ ];
              type = listOf str;
              description = "List of arguments to pass to the formatter";
            };
          };
        });

      example = [
        {
          package = pkgs.nixfmt;
          name = "nixfmt";
          command = "nixfmt";
          includes = [ "*.nix" ];
          arguments = [ ];
        }
      ];

      default = [ ];
      description = "List of formatters to be installed and configured";
    };

    globalExcludes = lib.mkOption {
      default = [ ];
      example = [
        "*.jpg"
        "*.png"
      ];
      type = lib.types.listOf lib.types.str;
      description = "List of globally ignored file types";
    };
  };

  config =
    let
      cfg = config.modules.treefmt;

      generateFormatterTOML = formatter: ''
        [formatter.${formatter.name}]
        command = "${formatter.command}"
        options = ${builtins.toJSON formatter.arguments}
        excludes = ${builtins.toJSON formatter.excludes}
        includes = ${builtins.toJSON formatter.includes}
      '';
      fullTOML = ''
        # This is a autogenerated file, do not edit manually, changes will be overwritten
        ${builtins.concatStringsSep "\n" (lib.map generateFormatterTOML cfg.additionalFormatters)}
        [global]
        excludes = ${builtins.toJSON cfg.globalExcludes}
      '';
      formatters = lib.map (p: p.package) cfg.additionalFormatters;
    in
    lib.mkIf cfg.enable {
      home.packages = [
        pkgs.treefmt
      ]
      ++ formatters;

      modules.fish.additionalShellAliases = lib.mkIf config.modules.fish.enable {
        treefmt-init = "echo '${fullTOML} > ./treefmt.toml";
        treefmt-global = "treefmt --tree-root . --config-file ~/.config/treefmt/global.toml";
      };

      xdg.configFile."treefmt/global.toml".text = fullTOML;
    };
}
